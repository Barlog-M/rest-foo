buildscript {
	ext.kotlinVersion = '1.0.5-2'
	ext.springBootVersion = '1.4.2.RELEASE'

	repositories {
		jcenter()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

plugins {
	// gradle dependencyUpdates -Drevision=release
	id 'com.github.ben-manes.versions' version '0.13.0'
	id 'com.palantir.docker' version '0.9.1'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'li.barlog.foo'
version = '0.1-SNAPSHOT'

mainClassName = 'li.barlog.AppKt'

repositories {
    jcenter()
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

	compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	compile('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
	}
	compile 'org.springframework.boot:spring-boot-starter-undertow'

	compile 'com.ibm.icu:icu4j:58.1'

	testCompile 'org.springframework.boot:spring-boot-starter-test'
}

jar {
	archiveName 'app.jar'
}

// https://github.com/palantir/gradle-docker
// gradle docker

docker() {
	bootRepackage

	name 'app'
	tags 'latest'
	dockerfile 'src/main/docker/Dockerfile'
	buildArgs([
		PORT     : "8080",
		JAVA_OPTS: "-Xms128m -Xmx256m"
	])
	pull true
}

wrapper {
	gradleVersion '3.2'
}

bootRepackage {
	it.mustRunAfter clean
}
